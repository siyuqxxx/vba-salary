'存储单子信息
Private cAcvmt() As New cAchievement

'存储单子总数
Private intAcvmtsCount As Integer

'存储业绩总数
Private dPorfitCount As Double

'存储正则表达式表
Private cRegList As New cRegexes

'============================================================== Set
'写入单子总数
Private Sub SetintAcvmtsCount(intInput As Integer)
    intAcvmtsCount = intInput
End Sub

'写入业绩总数
Private Sub SetdPorfitCount()
    Dim a As cAchievement
    Dim i As Integer
    
    For i = 1 To intAcvmtsCount
        dPorfitCount = cAcvmt(i).GetProfit() + dPorfitCount
    Next i
End Sub

Private Sub SetcRegList()
'    遍历表，获取表中所有正则表达式信息
'
'    参数:
'    NULL
'
'    返回:
'    NULL
    
    cRegList.SetRegItem
End Sub

'============================================================== Get

Public Function GetdPorfitCount() As Double
'    获取总业绩
'
'    参数:
'    NULL
'
'    返回:
'    GetdPorfitCount() As Double

    GetdPorfitCount = dPorfitCount
End Function

Public Function GetintAcvmtsCount() As Integer
'    获取总单子数
'
'    参数:
'    NULL
'
'    返回:
'    GetintAcvmtsCount() As Integer

    GetintAcvmtsCount = intAcvmtsCount
End Function

'==============================================================

'写入单子信息
Public Sub SetAcvmtsItemFormExcel()
'    遍历 Excel 表中的 业绩表，获取表中所有单子信息
'
'    参数:
'    objList As ListObjects
'
'    返回:
'    NULL
    
    '从 Excel 中，读取正则表达式计算表
    cRegList.SetRegItemFormExcel
    
    Dim wrkshtBase As Worksheet
    Dim objList As ListObject
    
    Set wrkshtBase = ActiveWorkbook.Worksheets("毛利总表")
    Set objList = wrkshtBase.ListObjects(1)
    
    
    Dim ListObjRows As ListRows
    Dim ListObjRow As ListRow
    
    Set ListObjRows = objList.ListRows
    ReDim cAcvmt(0)
    Dim i As Integer
    i = 1
    
    For Each ListObjRow In ListObjRows
        Dim SetFlag As Boolean
        
        ReDim Preserve cAcvmt(i)
        cAcvmt(i).SetcRegList cRegList
        
        SetFlag = cAcvmt(i).SetAchievementFromList(ListObjRow)
         
        
        If SetFlag Then
'            cAcvmt(i).ShowAchievementInfo
            i = i + 1
        End If
        
    Next ListObjRow
    
    '写入单子总数
    SetintAcvmtsCount (i)
    
    '写入业绩总数
    SetdPorfitCount
    
End Sub

'
Public Sub AddAcvmtsItem(cAcvmtInput As cAchievement, Sale As Integer)
'    写入单子信息
'    将 指定的 cAcvmtInput 追加到 cAcvmt() 最末尾
'    并修改计算intAcvmtsCount（总单子数），dPorfitCount（总业绩数），cRegList（存储正则表达式表）
'
'    参数:
'    cAcvmtInput as cAchievement
'
'    返回:
'    NULL
    
On Error Resume Next

    Dim intCount As Integer
    If UBound(cAcvmt) = -1 Then
        lCount = UBound(cAcvmt) + 1
        ReDim Preserve cAcvmt(lCount)
    End If
    
    intCount = UBound(cAcvmt) + 1
    
    
    ReDim Preserve cAcvmt(intCount)
    
    cAcvmt(intCount).CopyAcvmt cAcvmtInput
    
    '更新总单子数
    intAcvmtsCount = intCount
    
    '更新业绩总数
    dPorfitCount = dPorfitCount + cAcvmtInput.GetIndividualPerformance(Sale)
    
End Sub

Public Function GetAchievement(intNum As Integer) As cAchievement
'    获取cAchievements中指定GetAchievement的内容，
'    类似数组
'
'    参数:
'    NULL
'
'    返回:
'    NULL
    Dim cAcvmtA As New cAchievement
    cAcvmtA.CopyAcvmt cAcvmt(intNum)
    Set GetAchievement = cAcvmtA
End Function

Public Sub PrintAcvmtsToExcel(wrkshtOutPut As Worksheet, intSale As Integer, strName As String)
'    逐条输出业绩信息到Excel
'
'    参数:
'    NULL
'
'    返回:
'    NULL

    Dim a As cAchievement
    Dim i As Integer
    
    For i = 1 To intAcvmtsCount
        cAcvmt(i).PrintAcvmt wrkshtOutPut, i + 1, intSale
    Next i
    
    '输出个人总业绩汇总
    wrkshtOutPut.Range(wrkshtOutPut.Cells(i + 1, 2), wrkshtOutPut.Cells(i + 1, 7)).Merge
    wrkshtOutPut.Range(wrkshtOutPut.Cells(i + 1, 2), wrkshtOutPut.Cells(i + 1, 7)) = strName & "总业绩"
    wrkshtOutPut.Cells(i + 1, 8) = dPorfitCount
    
    '设置表格格式
    SetExcelFormat wrkshtOutPut
End Sub

Public Function GetIndividualPerformanceLists(intTestID As Integer) As cAchievements
'    遍历当前的cAchievements,提取包含指定团号cAchievement,存储在一个新的cAchievements中
'
'    参数:
'    intTestID As Intege
'
'    返回:
'    GetIndividualPerformanceLists As cAchievements()

    Dim cAcvmtsA As New cAchievements
    
    Dim i As Integer
    For i = 1 To intAcvmtsCount
        If cAcvmt(i).IsMyTrip(intTestID) Then
            cAcvmtsA.AddAcvmtsItem cAcvmt(i), intTestID
        End If
    Next i
    
    '备份cRegList到新的cAchievements中
    cAcvmtsA.CopycRegList cRegList
    
    Set GetIndividualPerformanceLists = cAcvmtsA
End Function

Public Sub CopycRegList(cRegsInput As cRegexes)
'    读出当前的cAchievements中的cRegList
'
'    参数:
'    NULL
'
'    返回:
'    GetcRegList As cRegexes

    cRegList.CopyRegexes cRegsInput
End Sub

Private Sub SetExcelFormat(ws As Worksheet)
'    设置cAchievements中各个员工的业绩导入到 Excel 表格中的格式
'
'    参数:
'    ws As Worksheet
'
'    返回:
'    NULL

    ws.Cells.Borders(xlDiagonalDown).LineStyle = xlNone
    ws.Cells.Borders(xlDiagonalUp).LineStyle = xlNone
    With ws.Cells.Borders(xlEdgeLeft)
        .LineStyle = xlContinuous
        .ColorIndex = 0
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With ws.Cells.Borders(xlEdgeTop)
        .LineStyle = xlContinuous
        .ColorIndex = 0
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With ws.Cells.Borders(xlEdgeBottom)
        .LineStyle = xlContinuous
        .ColorIndex = 0
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With ws.Cells.Borders(xlEdgeRight)
        .LineStyle = xlContinuous
        .ColorIndex = 0
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With ws.Cells.Borders(xlInsideVertical)
        .LineStyle = xlContinuous
        .ColorIndex = 0
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With ws.Cells.Borders(xlInsideHorizontal)
        .LineStyle = xlContinuous
        .ColorIndex = 0
        .TintAndShade = 0
        .Weight = xlThin
    End With
    With ws.Cells.Font
        .Name = "宋体"
        .Size = 8
        .Strikethrough = False
        .Superscript = False
        .Subscript = False
        .OutlineFont = False
        .Shadow = False
        .Underline = xlUnderlineStyleNone
        .ThemeColor = xlThemeColorLight1
        .TintAndShade = 0
        .ThemeFont = xlThemeFontMinor
    End With
    
    ws.Cells.ColumnWidth = 8.38
End Sub
